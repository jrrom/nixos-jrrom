#!/usr/bin/env fish

# Home Switch ETC CLI

function help
    echo
    echo "HSETC - HOME SWITCH ETC : jrrom 2025"
    echo
    echo (set_color yellow)"hswitch"(set_color normal)" - home-manager switch --flake .#void"
    echo (set_color yellow)"pushetc"(set_color normal)" - "(set_color red)"(sudo) "(set_color normal)"pushes void/etc files to /etc"
    echo (set_color yellow)"grubber"(set_color normal)" - "(set_color red)"(sudo) "(set_color normal)"pushes grub config to /etc/default, also mkconfig"
    echo (set_color yellow)"dracuts"(set_color normal)" - "(set_color red)"(sudo) "(set_color normal)"adds gruvbox modules and rebuilds"
    echo (set_color yellow)"runitsc"(set_color normal)" - "(set_color red)"(sudo) "(set_color normal)"pushes to /etc/runit/core-services"
    echo
end

function getDir
    echo (status dirname)
end

function pushetc
    set -l local_greetd "$(getDir)/void/etc/greetd"
    set -l local_swaylock "$(getDir)/void/etc/zzz.d/suspend/10-swaylock.sh"

    sudo cp -r $local_greetd/* /etc/greetd
    if test $status -ne 0
        echo "Error: Failed to copy greetd files!" >&2
        return 1
    end
    sudo cp -r $local_swaylock /etc/zzz.d/suspend
    if test $status -ne 0
        echo "Error: Failed to copy 10-swaylock-suspend file!" >&2
        return 1
    end
end

function grubber
    set -l local_grub "$(getDir)/void/etc/default/grub"
    sudo cp $local_grub /etc/default/grub
    if test $status -ne 0
        echo "Error: Failed to copy grub file!" >&2
        return 1
    end
    
    sudo grub-mkconfig -o /boot/grub/grub.cfg
    if test $status -ne 0
        echo "Error: Failed to generate grub config!" >&2
        return 1
    end
end

function dracuts
    set -l local_dracuts "$(getDir)/void/usr/lib/dracut/modules.d"
    sudo cp -r $local_dracuts/* /usr/lib/dracut/modules.d/
    if test $status -ne 0
        echo "Error: Failed to copy modules!" >&2
        return 1
    end

    sudo dracut --force
    if test $status -ne 0
        echo "Error: Failed to dracut --force"
        return 1
    end
end

function runitsc
    set -l local_runitsc "$(getDir)/void/etc/runit/core-services"
    sudo cp -r $local_runitsc/* /etc/runit/core-services/
    if test $status -ne 0
        echo "Error: Failed to copy core services!" >&2
        return 1
    end
end

function try
    echo "$(getDir)/void/etc/default/grub"
    echo "$(getDir)/void/etc/greetd"
    echo "$(getDir)/void/etc/zzz.d"
    echo "$(getDir)/void/usr/lib/dracut/modules.d"
    echo "$(getDir)/void/etc/runit/core-services"
end

function main -d "Controls Home Manager and ETC files"
    if test -z "$argv"
        echo "Error: No command provided." >&2
        help
        return 1
    end
    
    set -l command $argv[1]

    switch $command
        case help
            help
        case hswitch
            home-manager switch --flake .#void
        case pushetc
            pushetc
        case grubber
            grubber
        case dracuts
            dracuts
        case runitsc
            runitsc
        case try
            try
        case "*"
            echo -e (set_color red)"Unknown command"(set_color normal)". Use \e[3m'hsetc help' for usage.\e[0m"
    end
end

main $argv
